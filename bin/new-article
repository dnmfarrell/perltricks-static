#!/usr/bin/env perl
use autodie;
use Cwd;
use Getopt::Long 'GetOptions';
use Time::Piece;

GetOptions(
  'title=s'       => \my $title,
  'description=s' => \(my $description = ''),
  'author=s'      => \(my $author = ''),
  'help'          => \&usage,
) or usage();
usage() unless $title;

# check we're in perltricks-static
my $cwd = cwd();
chomp $cwd; # for Mac OS?
my $parent_dir = [split /\/|\\/, $cwd]->[-1];
die "$0 must be run from the root project directory!\n"
  unless $parent_dir eq 'perltricks-static';

# prepare the file
my $filename = lc $title;
$filename =~ s/\W/-/g;
$filename = "content/article/$filename.md";
die "$filename.md already exists!\n" if -e $filename;
open my $fh, '>:utf8', $filename;

my $dt = localtime;
my $date = $dt->datetime;

print $fh qq(
  {
    "title" : "$title",
    "author": ["$author"],
    "date"  : "$date",
    "tags"  : [],
    "draft" : true,
    "description" : "$description",
    "image" : ""
  }

The article body goes here. Use [GitHub Flavored Markdown](https://guides.github.com/features/mastering-markdown/) to write the article body. Don't forget to delete this line!

);

print "$filename created\n.";

sub usage {
  print "\n$0 [OPTIONS]\n\n";
  print "Options
  --title, -t        Article title (required)
  --description, -d  Description (subtitle)
  --author, -a       Author
  --help, -h         Print help
  \n";
  exit 0;
}
